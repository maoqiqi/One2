// 全局配置信息及第三方依赖统一管理
project.ext {
    setDefaultConfig = { extension ->
        android {
            compileSdkVersion Integer.parseInt(COMPILE_SDK_VERSION)
            buildToolsVersion BUILDTOOLS_VERSION

            defaultConfig {
                minSdkVersion Integer.parseInt(MIN_SDK_VERSION)
                targetSdkVersion Integer.parseInt(TARGET_SDK_VERSION)
                versionCode 1
                versionName "1.0.0"
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                multiDexEnabled true
            }

            // ARouter路由配置
            kapt {
                arguments {
                    arg("AROUTER_MODULE_NAME", project.getName())
                }
            }

            sourceSets {
                main {
                    if (IS_MODULE.toBoolean() && file('src/main/module/AndroidManifest.xml').exists()) {
                        manifest.srcFile 'src/main/module/AndroidManifest.xml'
                    } else {
                        manifest.srcFile 'src/main/AndroidManifest.xml'
                    }
                }
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            kotlinOptions {
                jvmTarget = '1.8'
            }
            buildFeatures {
                dataBinding = true
            }
        }

        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            implementation 'androidx.core:core-ktx:1.3.2'
            implementation 'androidx.multidex:multidex:2.0.1'
            // ARouter
            implementation 'com.alibaba:arouter-api:1.5.1'
            kapt 'com.alibaba:arouter-compiler:1.5.1'
            implementation 'com.codearms.maoqiqi:lib-log:1.0.1'
        }
    }
}